#!/usr/bin/env perl

=pod

=head1 NAME

kvpairs2td - Transform lines of key-value pairs to tabular data stream

=head1 OPTIONS

=over 4

=item -i, --ignore-non-existing-columns

Do not fail when encounters a new field after the first record.

=item -w, --warn-non-existing-columns

=item -c, --column I<COLUMN>

Indicate that there will be a column by the name I<COLUMN>.
This is useful if the first record does not have I<COLUMN>.
This option is repeatable.

=item -r, --restcolumn I<NAME>

Name of the column where the rest of the input line will be put
which is not part of key-value pairs.
Default is B<_REST>.

=item -u, --unknown-to-rest

Put unknown (non-existing) fields in the "rest" column
(see B<-r> option).

=back

=head1 SEE ALSO

td2mrkv(1), td2kvpairs(1)

=cut


$OptWarnBadColumnNames = 1;
$OptFailBadColumnNames = 1;
@OptPredefColumns = ();
$OptRestColumnName = "_REST";
$OptUnknownToRest = 0;
%OptionDefs = (
	'i|ignore-non-existing-columns' => sub { $OptFailBadColumnNames = 0; $OptWarnBadColumnNames = 0; },
	'w|warn-non-existing-columns' => sub { $OptFailBadColumnNames = 0; $OptWarnBadColumnNames = 1; },
	'c|column=s@' => \@OptPredefColumns,
	'r|restcolumn=s' => \$OptRestColumnName,
	'u|unknown-to-rest' => \$OptUnknownToRest,
);

use Data::Dumper;
no if ($] >= 5.018), 'warnings' => 'experimental::smartmatch';
do '/usr/lib/tool/perl5/tabdata/common.pl' or die "$@";

@Headers = ();
$RN = 0;
$rest_column_idx = undef;

while(my $Line = <STDIN>)
{
	chomp $Line;
	my @record = ();
	my $Rest = '';
	
	while($Line =~ /(^|(?'spacebefore'\s*))((?'keyquote'[""''])(?'key'.+?)\g{keyquote}|(?'key'[^\s=]+))=((?'valuequote'[""''])(?'value'.*?)\g{valuequote}|(?'value'\S*))((?'spaceafter'\s*)|$)/)
	{
		#warn Dumper [$`, \%+, $'];
		$Rest .= $` . $+{'spacebefore'};
		$Line = $';
		my ($key, $value) = (kvpair_unescape($+{'key'}), kvpair_unescape($+{'value'}));
		my $spaceafter = $+{'spaceafter'};
		
		if($RN == 0)
		{
			push @Headers, $key;
		}
		
		my $cell_set = 0;
		my $colinstances = 0;
		for my $colidx (0 .. $#Headers)
		{
			if($Headers[$colidx] eq $key)
			{
				$colinstances++;
				if(not defined $record[$colidx])
				{
					$record[$colidx] = $value;
					$cell_set = 1;
					last;
				}
			}
		}
		
		if(not $cell_set)
		{
			$colinstances++;
			warn "$0: column (instance $colinstances) is not initialized: $key\n" if $OptWarnBadColumnNames;
			if($OptUnknownToRest)
			{
				$Rest .= kvpair_escape($key) . '=' . kvpair_escape($value) . $spaceafter;
			}
			else
			{
				exit 3 if $OptFailBadColumnNames;
			}
		}
	}
	
	if($RN == 0)
	{
		push @Headers, grep { not $_ ~~ @Headers } @OptPredefColumns, $OptRestColumnName;
		$rest_column_idx = $#Headers;
		print join($FS, @Headers).$RS;
	}
	
	$record[$rest_column_idx] = $Rest . $Line;
	
	print join($FS, map {escape_tabdata($_)} @record) . $RS;
	
	$RN++;
}
