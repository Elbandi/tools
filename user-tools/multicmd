#!/usr/bin/env perl

=pod

=head1 NAME

multicmd - Run multiple commands in series

=head1 SYNOPSIS

multicmd I<COMMAND-1> I<ARGS-1> ";" I<COMMAND-2> I<ARGS-2> ";" ...

Run I<COMMAND-1>, I<COMMAND-2>, ... I<COMMAND-n> after each other,
similarly like shells would do, except not involving any shell.

=head1 OPTIONS

=over 4

=item -d, --delimiter I<STRING>

Set command delimiter to I<STRING>.
Default is a literal C<;> semicolon.
Probably need to shell-escape.

=back

=head1 CAVEATS

Note, that C<;> (or the non-default delimiter set by B<--delimiter>) is a shell meta-char
in your shell, so you need to escape/quote it, but it's a separate literal argument
when you call multicmd(1) in other layers (eg. execve(2)),
so don't just stick to the preceding word. Ie:

B<WRONG>: multicmd date\; ls

B<WRONG>: multicmd 'date; ls'

B<WRONG>: multicmd 'date ; ls'

B<CORRECT>: multicmd date \; ls

B<CORRECT>: multicmd date ';' ls

=head1 EXIT STATUS

multicmd(1) exit with the exit code of the last command.

=cut

use Getopt::Long qw/:config no_ignore_case bundling no_getopt_compat/;
use Pod::Usage;
use Data::Dumper;
use POSIX;

$Delimiter = ';';

sub parameter_error
{
	pod2usage(-exitval=>2, -verbose=>99);
}

while(@ARGV)
{
	my $arg = shift @ARGV;
	if(my ($opt, $optval) = $arg =~ /^(-d|--delimiter)(?:=(.+)|)$/)
	{
		if(not defined $optval)
		{
			$optval = shift @ARGV or parameter_error();
		}
		$Delimiter = $optval;
	}
	elsif($arg =~ /^--help$/)
	{
		pod2usage(-exitval=>0, -verbose=>99);
	}
	elsif($arg =~ /^-/)
	{
		warn "$0: unknown option: $arg\n";
		parameter_error();
	}
	else
	{
		unshift @ARGV, $arg;
		last;
	}
}

if(not @ARGV)
{
	parameter_error();
}

@cmd_args = ([]);
$cmd_idx = 0;

for my $arg (@ARGV)
{
	if($arg eq $Delimiter)
	{
		$cmd_idx++;
		$cmd_args[$cmd_idx] = [];
	}
	else
	{
		push @{$cmd_args[$cmd_idx]}, $arg;
	}
}

$last_cmd_ref = pop @cmd_args;

for my $cmd_ref (@cmd_args)
{
	system @$cmd_ref;
}

exec @$last_cmd_ref;

# if command not found:
exit 127;
